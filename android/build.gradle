apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  namespace "com.jansoft.mbukanji.valhalla"
  compileSdkVersion safeExtGet('compileSdkVersion', 34)
  ndkVersion safeExtGet('ndkVersion', '26.1.10909125')

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 24)
    targetSdkVersion safeExtGet('targetSdkVersion', 34)
    versionCode 1
    versionName "0.1.0"
    externalNativeBuild {
      cmake {
        cppFlags "-std=c++17"
        arguments "-DANDROID_STL=c++_shared"
        if (project.hasProperty('valhallaPrefix')) {
          arguments "-DVALHALLA_PREFIX=${project.valhallaPrefix}"
        }
      }
    }
    ndk {
      abiFilters "arm64-v8a"
    }
  }

  externalNativeBuild {
    cmake {
      path "src/main/cpp/CMakeLists.txt"
      version safeExtGet('cmakeVersion', '3.22.1')
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    main {
      java.srcDirs = ['src/main/java']
    }
  }
}

dependencies {
  implementation "com.facebook.react:react-android"
}
